plugins {
    id 'java'
    id 'application'
}

ext {
    moduleName = 'org.mrmat.hello.java.modules.app'
}
mainClassName = "$moduleName/org.mrmat.hello.java.modules.app.Greeter"

dependencies {
    compile project(":java9-modules-api")
    runtime project(':java9-modules-english')
    runtime project(':java9-modules-german')

    testCompile("org.junit.jupiter:junit-jupiter-api:5.1.0")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.1.0")
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'org.junit.jupiter.api',
                '--add-reads', "$moduleName=org.junit.jupiter.api",
                '--add-exports', "$moduleName/org.mrmat.hello.java.modules.app.test=org.junit.platform.commons",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'ALL-MODULE-PATH',
                '--add-reads', "$moduleName=org.junit.jupiter.api",
                '--add-exports', "$moduleName/org.mrmat.hello.java.modules.app.test=org.junit.platform.commons",
                '--add-opens', "$moduleName/org.mrmat.hello.java.modules.app.test=org.junit.platform.commons",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
        ]
        classpath = files()
    }

    useJUnitPlatform()
    testLogging {
        events = [ "skipped", "passed", "failed" ]
    }
}

run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--module', mainClassName
        ]
        classpath = files()
    }
}